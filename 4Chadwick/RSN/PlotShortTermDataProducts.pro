;
; File: PlotShortTermDataProducts.pro
;
; This IDL program will display the the Short-Term Data Products:
; The Depth differences between 2 data points in 5 minute apart
; and the Average Depth differences between 2 consective 10-minute
; averaged depths.
; These products are generated by programs: CheckNANOdata4Alerts.pro
; or GetDetectionParameters.pro  using the NANO data.
;
; This program will require the procerdures in the files:
; GetLongTermNANOdataProducts.pro,
; PlotNANOdata.pro, PlotRSNdata.pro, and SplitRSNdata.pro
;
; Programmer: T-K Andy Lau NOAA/PMEL/Acoustic Program HMSC Newport Oregon.
;
; Revised on January   20th, 2015
; Created on December   9th, 2014
;

;
; This procedure will display all the data in the SHORT_TERM_DATA_FILE
; The data are: Depth Change with 5 minutes, Depth Differences of two
; consective 10-minute averaged depths.
; 
; Callers: PROCESS_RSN_DATA and Users. 
;
PRO PLOT_SHORT_TERM_DATA, SHORT_TERM_DATA_FILE,  $ ; Input: File name.
     WHAT2PLOT,  $ ; Input: 2-Element integer to indicate what to plot.
      SHOW_PLOT=DISPLAY_PLOT2SCREEN,  $ ; Show the plot in the display window.
    UPDATE_PLOT=SAVE_PLOTS              ; 0=Not Save (default) & 1=Save
;
; WHAT2PLOT = [ Short Term Plot, Long Term Plot ] indicators.  For example,
; WHAT2PLOT = [ -3, 1 ]  means Plot the last 3 days, data & All the data.
; When zero value is used.  That means No Plotting for the respected term.
;
FILE = FILE_INFO( SHORT_TERM_DATA_FILE )  ; Get the File's information.
;  
IF NOT FILE.EXISTS THEN  BEGIN  ; The IDL_FILE cannot be found.
   PRINT, 'From PLOT_SHORT_TERM_DATA, the File: ', SHORT_TERM_DATA_FILE
   PRINT, 'Cannot be found.  No figures are created.'
   RETURN  ; to Caller.
ENDIF
;
;  The SHORT_TERM_DATA_FILE is Found.
;
IF NOT KEYWORD_SET( SAVE_PLOTS ) THEN  BEGIN
   SAVE_PLOTS = 0B  ; Not Save.
ENDIF
;
IF NOT KEYWORD_SET( DISPLAY_PLOT2SCREEN ) THEN  BEGIN
   DISPLAY_PLOT2SCREEN = BYTE( 0 )  ; No.
ENDIF
;
; Get the File ID from the 1st 5 characters of the SHORT_TERM_DATA_FILE name.
; which will be '/RSN/MJ03F/EventDetectionParameters.MJ03F' for example.
; and the ID will the 'MJ03F'.
; 
; N  = STRLEN( SHORT_TERM_DATA_FILE )
; ID = STRMID( SHORT_TERM_DATA_FILE, N-5, 5 )  ; = 'MJ03F' e.g.
;
; Get the File ID from the 1st 5 characters of the SHORT_TERM_DATA_FILE name.
; which will be '/RSN/MJ03F/EventDetectionParametersMJ03F.idl' for example.
; and the ID will the 'MJ03F'.
;
  N  = STRLEN( SHORT_TERM_DATA_FILE )
  ID = STRMID( SHORT_TERM_DATA_FILE, N-9, 5 )  ; = 'MJ03F' e.g.
;
  HELP, ID
;
; Define the file names for the storing the plotted data.
;
IF SAVE_PLOTS THEN  BEGIN
   SHRT3DAYPNG_FILE = ID + '-STrates-3day.png'  ; Graphic files' names for
   SHRTALL_PNG_FILE = ID + '-STrates-All.png'   ; Short-Term Data Plots.
ENDIF  ELSE  BEGIN
   SHRT3DAYPNG_FILE = 'Do Not Save'         ; Indicate the plotted displays
   SHRTALL_PNG_FILE = 'Do Not Save'         ;
ENDELSE
;
; Get All the DATA in the SHORT_TERM_DATA_FILE.
;
; Note that the GET_DETECTED_RATES & RETRIEVE_DETECTED_RATES procedures
; are in the file: GetShortTermNANOdataProducts.pro
;
; GET_DETECTED_RATES has been used in between
; November 2014 & January 23rd, 2015.
; GET_DETECTED_RATES,      SHORT_TERM_DATA_FILE, DATA, STATUS
; it reads the DATA from the Text File: SHORT_TERM_DATA_FILE.
;
; Reads the DATA from the IDL Save File: SHORT_TERM_DATA_FILE
; (Stared using it on January 23rd, 2015).
;
  RETRIEVE_DETECTED_RATES, SHORT_TERM_DATA_FILE, DATA, STATUS
;
; Note that the DATA will be 2-D array of N x 3 and
; DATA[*,0] = Date and Time in JULDAY() values.
; DATA[*,1] = Depth Differences in cm/5-minute.
; DATA[*,2] = Depth Differences in cm/hour of two 10-minute averaged Depths.
;
  T = SIZE( DATA, /DIMENSION )  ; Get DATA's dimensional sizes.
  N = T[0]   ; 1st Dimension and T[1] = 3.
  N = N - 1  ; Use as the last 1st dimensional index for DATA.
;
; Get the Current Directory Name
; and where the output graphic files will be stored.
;
  CD, CURRENT=CURRENT_DIR            ; Get the Current Directory name.
  CD, CURRENT_DIR + PATH_SEP() + ID  ; e.g. '/RSN/MJ03D'
;
  TITLE4PLOT = 'RSN-' + ID  $  ; ID = 'MJ03D', 'MJ03E' or 'MJ03F'
             + ' Short-Term Rates (Depth Changes), '
  DAY_OFFSET = WHAT2PLOT[0]  ; e.g -3 = Plot the last 3 days, data.
  LONG_TERM  = WHAT2PLOT[1]  ; e.g  1 = Plot All the data.
;
IF LONG_TERM  GT 0 THEN  BEGIN  ; Plot the Long  Term Data Set.
   PLOT_DEPTH_CHANGE_RATES,  SHOW_PLOT=DISPLAY_PLOT2SCREEN,   $
                     TITLE=TITLE4PLOT + 'Start to Present.',  $
                     DATA[0:N,0], DATA[0:N,1], DATA[0:N,2] ,  $ ; as
;                    Time,        5-minute   & 10-min differences.
                     SHRTALL_PNG_FILE  ; File for storing the figure.
ENDIF
;
IF DAY_OFFSET LT 0 THEN  BEGIN  ; Plot the last few days of the data.
   T = JDAY_TIME_INDEX( DATA[0:N,0], DAY_OFFSET )
   IF T GT 0 THEN  BEGIN  ; Time Index is found.
      TITLE4PLOT += 'last ' + STRTRIM( ABS( DAY_OFFSET ), 2 )  $
                            + ' days or more.'
   ENDIF  ELSE  BEGIN  ; Time Index is Not found.
      T =  0  ; All data will be plotted.
      TITLE4PLOT += 'Start to Present.'
   ENDELSE
   PLOT_DEPTH_CHANGE_RATES, SHOW_PLOT=DISPLAY_PLOT2SCREEN, TITLE=TITLE4PLOT,  $
                     DATA[T:N,0], DATA[T:N,1], DATA[T:N,2],  $  ; as
;                    Time,        cm/5-minute  & cm/hour.
                     SHRT3DAYPNG_FILE  ; File for storing the figure.
ENDIF
;
  CD, CURRENT_DIR  ; Move the directory back to where it came from.
;
RETURN
END  ; PLOT_SHORT_TERM_DATA
;
; Callers: PLOT_SHORT_TERM_DATA or Users.
; Revised: January 20th, 2015
;
PRO PLOT_DEPTH_CHANGE_RATES,  TIME,  $ ; Input: 1-D array of JULDAY().
    R05M,  $ ; Input: 1-D array of Depth Change Rate in cm/5-minute.
    R10M,  $ ; Input: 1-D array of Depth Change Rate in cm/hour.
    OUTPUT_FILE,  $ ; for the plotted graph as *.png file. 
    SHOW_PLOT=DISPLAY_PLOT2SCREEN,  $ ; Show the plot in the display window.
    TITLE    =TITLE4PLOT              ; e.g. 'RSN-MJ03F Short-Term Rates.'
;
IF DISPLAY_PLOT2SCREEN THEN  BEGIN
   WINDOW, /FREE, RETAIN=2, XSIZE=800, YSIZE=256
   PRINT, 'Plotting Window: ', !D.WINDOW
ENDIF  ELSE  BEGIN  ; will plot the graph into a PIXMAP window.
   WINDOW, /FREE, RETAIN=2, XSIZE=800, YSIZE=256, /PIXMAP
   PRINT, 'PIXMAP Window: ', !D.WINDOW 
ENDELSE
;
IF NOT KEYWORD_SET( TITLE4PLOT ) THEN  BEGIN
   TITLE4PLOT = 'RSN Short-Term Rates (Depth Changes)'
ENDIF
;
; The following 2 procedures are in the file IDLcolors.pro
;
  SET_BACKGROUND, /WHITE  ; Plotting background to White.
  RGB_COLORS,      C      ; Color name indexes, e.g. C.BLUE, C.GREEN, ... etc.
;
; Convert the 5-minute Depth Changes values (R05M) in cm/(5 minutes)
; into cm/minute.  The conversion factor will be equal to 1/5 as
; (cm/5 minutes)/5.
;
  RATE05 = R05M/5.0D0
;
; No conversion for the Depth Changes Rate (R10M).  It is always in cm/hr.
;
  RATE10 = TEMPORARY( R10M )
;
; Determine number of Hourly or Daily marks per day.
;
  N      =   N_ELEMENTS( TIME )
  N_DAYS = ( TIME[N-1] - TIME[0] )  ; Data Range in days.
;
; The following Procedure: RSN_GET_XTICK_UNITS is located at the
; file: PLOT_NANOdata.pro.
;
  RSN_GET_XTICK_UNITS, N_DAYS, XUNITS, H
;
; Where XUNITS will be returned as ['Hour','DAY'] or ['Day', 'DAY']
; and   H is the XTICKINTERVAL=H for either Hour or Day.
;
IF XUNITS[0] EQ 'Day'  THEN  BEGIN
   TIME_RANGE = LABEL_DATE( DATE_FORMAT=['%D','%N/%D/%Z'] )
ENDIF  ELSE  BEGIN  ; XUNITS[0]  == 'Day'
   IF N_DAYS LT 5 THEN  BEGIN
      TIME_RANGE = LABEL_DATE( DATE_FORMAT=['%H','%N/%D/%Z'] )
   ENDIF  ELSE  BEGIN
      XUNITS[0]  = 'Day'  ; Change from 'Hour' to 'Day'
      IF N_DAYS LE 10 THEN  BEGIN
         H = 1
      ENDIF ELSE IF N_DAYS LE 30 THEN  BEGIN
         H = 10
         H =  5
      ENDIF
      TIME_RANGE = LABEL_DATE( DATE_FORMAT=['%D','%N/%D/%Z'] )
   ENDELSE
ENDELSE
;
; Call LABEL_DATE() to ask for plotting Time Label
; as Hour, Month/Day/Year or Day, Month/Day/Year
;
  TIME_RANGE  = STRING( TIME[0],   $ ; Date & Time of the 1st data point.
  FORMAT='(C(CMOI,"/",CDI2.2,"/",CYI,X,CHI2.2,":",CMI2.2,":",CSI2.2))' )
  TIME_RANGE += ' to ' + STRING( TIME[N-1],  $  ; to the Last data point.
  FORMAT='(C(CMOI,"/",CDI2.2,"/",CYI,X,CHI2.2,":",CMI2.2,":",CSI2.2))' )
;
; Define a plotting symbel: a dot when using PSYM=8.
;
  USERSYM, [-0.2,0.2],[0,0]
  S = FINDGEN( 17 )*!PI/8.0 
  USERSYM, COS( S ), SIN( S ), /FILL  ;, THICK=2
;
; Define the Start and End Time Range for plotting.
;
  CALDAT, TIME[0], MN, N_DAYS, Y           ; Get Month, Day and Year.
       S = JULDAY( MN, N_DAYS, Y, 0,0,0 )  ; The Beginning of the day.
;
          MX = TIME[N-1] + 1               ; Move to the next day.
  CALDAT, MX,      MN, N_DAYS, Y           ; Get Month, Day and Year.
  N_DAYS = JULDAY( MN, N_DAYS, Y, 0,0,0 )  ; The Beginning of the next day.
;
; Define the Hardware Fonts to be used.
;
  DEVICE, FONT='-adobe-helvetica-bold-r-normal--12-120-75-75-p-70-iso8859-1'
;
 !P.FONT  = 0  ; Use the hardware font above.
 !X.THICK = 1
 !Y.THICK = 1  ; for thinker line drawing.
;
; Get the Max & Min of the 10-minute Depth Change data range.
;
  MX = MAX( RATE10, MIN=MN )  ;
  MX =  CEIL( MX )
  MN = FLOOR( MN )
  HELP, MX, MN
;
  PRINT, 'For 10-minute Depth Change Rates:'
  HELP, MX,MN, M
;
; Adjust the Rate Range.
;
  IF MX LT  5.0 THEN MX =  5.0
  IF MN GT -5.0 THEN MN = -5.0
;
; Display the 10-minute average Depth Change values in cm/hour unit.
;
 !P.FONT  = 0  ; Use the hardware font above.
  PLOT, TIME, RATE10,  /NODATA,                         $
        YSTYLE=2+4, YRANGE=[MN,MX],    YMARGIN=[ 6,2],  $
        XSTYLE=1,   XRANGE=[S,N_DAYS], XMARGIN=[12,8],  $
        XTICKFORMAT=['LABEL_DATE','LABEL_DATE'], XTICKUNITS=XUNITS,  $
        XTITLE=TIME_RANGE, XTICKINTERVAL=H, TITLE=TITLE4PLOT
 OPLOT, TIME, RATE10, COLOR=C.RED, PSYM=8  ; Plot data in dots.
  AXIS, YAXIS=1, YRANGE=[MN,MX], YSTYLE=2, COLOR=C.RED
 XYOUTS, ALIGNMENT=1, /DEVICE, 45, 45, XUNITS[0] + ':'  ; Hour or Day.
 !P.FONT = -1  ; Back to graphic font.
 XYOUTS, CHARSIZE=1.00, CHARTHICK=1, COLOR=C.RED, ORIENTATION=90,  $
         /DEVICE, 780,  70, '!1720-min. Rate (cm/hour)' ; Right Y-Axis Label.
;
; Get the Max & Min of the 5-minute Depth Change data range.
;
  MX = MAX( RATE05, MIN=MN )  ; in cm/minute.
;
  PRINT, 'For 5-minute Depth Change Rates:'
  HELP, MX,MN, M
;
; Adjust the Rate Range.
;
  IF MX LT  1.0 THEN MX =  1.0
  IF MN GT -1.0 THEN MN = -1.0
;
; Display the  5-minute Depth Change values in cm/minute unit.
;
 !P.FONT  = 0  ; Use the hardware font above.
  PLOT, TIME, RATE05,  /NODATA,     /NOERASE,           $
        YRANGE=[MN,MX], YSTYLE=2+4,  YMARGIN=[ 6,2],    $
        XSTYLE=1, XRANGE=[S,N_DAYS], XMARGIN=[12,8],    $
;       XTICKFORMAT=['LABEL_DATE','LABEL_DATE'], XTICKUNITS=['Hour','DAY'], $
        XTICKFORMAT=['LABEL_DATE','LABEL_DATE'], XTICKUNITS=XUNITS,  $
        XTITLE=TIME_RANGE, XTICKINTERVAL=H, TITLE=TITLE4PLOT
;  e.g. TITLE4PLOT='RSN Short-Term Rates (Depth Changes), last 3 days.'
;
  OPLOT, TIME, RATE05, COLOR=C.GREEN, PSYM=8  ; Plot data points in Dots.
   AXIS, YAXIS=0, YRANGE=[MN,MX], YSTYLE=2, CHARSIZE=1.15, COLOR=C.GREEN
;
 !P.FONT = -1  ; Back to graphic font.
 XYOUTS, CHARSIZE=1.00, CHARTHICK=1, COLOR=C.GREEN, ORIENTATION=90,  $
         /DEVICE, 25, 70, '!175-min. Rate (cm/minute)' ; Left Y-Axis Label.
;
; If the OUTPUT_FILE name is Not = 'Do Not Save',
; Save the graph into the output file.
;
IF ( OUTPUT_FILE NE 'Do Not Save' ) AND ( OUTPUT_FILE NE '' ) THEN  BEGIN
   WRITE_PNG, OUTPUT_FILE, TVRD( TRUE=1 )  ; Save the graph as a png file.
ENDIF
;  
IF NOT DISPLAY_PLOT2SCREEN THEN  BEGIN
       MX = !D.WINDOW
   WDELETE, !D.WINDOW  ; Remove the PIXMAP window.
   PRINT, 'Removed PIXMAP Window: ', MX
ENDIF
;
RETURN
END  ; PLOT_DEPTH_CHANGE_RATES
