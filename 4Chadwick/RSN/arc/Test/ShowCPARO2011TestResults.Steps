;
; File: ShowCPARO2011TestResults.Steps
;
; Display the results from the TestNANOdata4Alerts.pro
; Revised: January 7, 2015

;
; Restrieve the Pressure Data used for testing.
; Array variables: BPR2011, BPR2011_NOTE, BTIME2011, DETIDED2011
;
  RESTORE, '~/4Chadwick/MARS2010-2011/CPARO2011Resample.idl'
;
; Restrieve the data results generated by the program: TestNANOdata4Alerts.pro
; Array variables:
; SUBSIDE,SUBSIDE_NOTE, TSUNAMI,TSUNAMI_NOTE, UPLIFT,UPLIFT_NOTE
;
  RESTORE, '~/4Chadwick/RSN/Test/TestCPARO2011AlertsDetect.idl'
;
 .RUN ~/idl/IDLcolors.pro
;
  WINDOW, RETAIN=2, XSIZE=1024, YSIZE=256
;
; The following 2 procedures are in the file IDLcolors.pro
;
  SET_BACKGROUND, /WHITE  ; Plotting background to White.
  RGB_COLORS,      C      ; Color name indexes, e.g. C.BLUE, C.GREEN, ... etc.
;
; Define an Upside Down Triangle symbol for plotting PSYM=8.
;
  USERSYM, [-1, 1, 0, -1 ], [1, 1, -1, 1]
;
; Determine number of Hourly or Daily marks per day.
;
  N      = N_ELEMENTS( BTIME2011  )
  N_DAYS = ( BTIME2011[N-1] - BTIME2011[0] )  ; Data Range in days.
;
; Call LABEL_DATE() to ask for plotting Time Label
; as Hour, Month/Day/Year or Day, Month/Day/Year
;
; TIME_RANGE  = LABEL_DATE( DATE_FORMAT=['%D','%N/%D/%Z'] )
  TIME_RANGE  = LABEL_DATE( DATE_FORMAT=['%M'] )
;
  TIME_RANGE  = STRING( BTIME2011[0],   $ ; Date & Time of the 1st data point.
        FORMAT='(C(CMOI,"/",CDI2.2,"/",CYI,X,CHI2.2,":",CMI2.2,":",CSI2.2))' ) 
  TIME_RANGE += ' to ' + STRING( BTIME2011[N-1],  $  ; to the Last data point.
        FORMAT='(C(CMOI,"/",CDI2.2,"/",CYI,X,CHI2.2,":",CMI2.2,":",CSI2.2))' )
;
; Define the Y ranges for plotting.
;
  MN = 870.35
  MX = 870.65
;
; Define the Hardware Fonts to be used.
;
  DEVICE, FONT='-adobe-helvetica-bold-r-normal--12-120-75-75-p-70-iso8859-1'
;
 !X.THICK = 2
 !Y.THICK = 2  ; for thinker line drawing.
 !P.FONT  = 0  ; Use the hardware font above.
;
; Display the temperature values 1st.
;
  PLOT, BTIME2011, DETIDED2011,           XMARGIN=[9,3],   $
        YRANGE=[MN,MX], YSTYLE=1,         YMARGIN=[5,2],   $
        XSTYLE=1,  XRANGE=[BTIME2011[0],BTIME2011[N-1]],   $
        XTICKFORMAT=['LABEL_DATE'],  XTICKUNITS='Months',  $
        XTITLE=TIME_RANGE, XTICKINTERVAL= 1,  $
  TITLE='Using CPARO2011 Detided Data for Testing the Alert Detections'
; 
; Mark the Detected Uplift Events' Times as Triangles in Red.
;
  R =  UPLIFT[*,1]/600 + 870.45  ; Scale the Rate for plotting.
 OPLOT,  UPLIFT[*,0], R, THICK=2, COLOR=C.RED , PSYM=5  ; 5 = Triangle.
; 
; Mark the Detected Subsidence Events' Times as Oreange Upside Down Triangles.
;
  R = SUBSIDE[*,1]/600 + 870.45  ; Scale the Rate for plotting.
 OPLOT, SUBSIDE[*,0], R, THICK=2, COLOR=C.ORANGE, PSYM=8  ; 8 = User Defined.
; 
; Mark the Detected Tsunami Times as X's in Blue.
;
  R = TSUNAMI[*,1]/100 + 870.45  ; Scale the Heights for plotting.
 OPLOT, TSUNAMI[*,0], R, THICK=2, COLOR=C.BLUE, PSYM=7  ; 7 = 'X'
;
   PLOTS, /DEVICE, 120, 220, PSYM=5, COLOR=C.RED ,   THICK=2
  XYOUTS, /DEVICE, 125, 215, 'Uplife Event', COLOR=C.RED
   PLOTS, /DEVICE, 120, 200, PSYM=8, COLOR=C.ORANGE, THICK=2
  XYOUTS, /DEVICE, 125, 195, 'Subsidence Event', COLOR=C.ORANGE
   PLOTS, /DEVICE, 120,  25, PSYM=7, COLOR=C.BLUE,   THICK=2
  XYOUTS, /DEVICE, 125,  20, 'Tsunami Event', COLOR=C.BLUE
;
 !P.FONT = -1  ; Back to graphic font.
 XYOUTS, CHARSIZE=1.25, CHARTHICK=1, ORIENTATION=90,  $ 
         /DEVICE, 15, 120,  '!17Meters'  ; Right Y-Axis's Label.
;
  WRITE_PNG, '~/4Chadwick/RSN/Test/CPARO2011Detect.png', TVRD( TRUE=1 )
;
; Done PLotting.
